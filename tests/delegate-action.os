#Использовать asserts

///////////////////////////////////////////////////////////////////////
// Тест класса Действие/Action
///////////////////////////////////////////////////////////////////////

Перем юТест;

////////////////////////////////////////////////////////////////////
// Программный интерфейс

Функция ПолучитьСписокТестов(ЮнитТестирование) Экспорт
	
	юТест = ЮнитТестирование;
	
	ВсеТесты = Новый Массив;
	
	ВсеТесты.Добавить("ТестДолжен_ВыполнитьДействиеОбъектаТеста");
	ВсеТесты.Добавить("ТестДолжен_ВыполнитьДействиеОбъектаТестаСНесколькимиПараметрами");
	ВсеТесты.Добавить("ТестНеДолжен_ВыполнитьДействиеОбъектаТеста");
	ВсеТесты.Добавить("ТестДолжен_ВыполнитьДействиеОбъектаДвижка");
	ВсеТесты.Добавить("ТестДолжен_ВыполнитьДействиеОбъектаДвижкаНаАнглийском");
	
	Возврат ВсеТесты;
	
КонецФункции

Процедура ТестДолжен_ВыполнитьДействиеОбъектаТеста() Экспорт

	юТест.ПодробныеОписанияОшибок(Истина);

	Действие = Новый Действие(ЭтотОбъект, "Исполнитель");
	Результат = Действие.Выполнить();

	Ожидаем.Что(Результат).Равно(111);
	
КонецПроцедуры

Процедура ТестДолжен_ВыполнитьДействиеОбъектаТестаСНесколькимиПараметрами() Экспорт

	юТест.ПодробныеОписанияОшибок(Истина);

	Действие = Новый Действие(ЭтотОбъект, "Исполнитель");
	Результат = Действие.Выполнить(200, 20);

	Ожидаем.Что(Результат).Равно(221);
	
КонецПроцедуры

Процедура ТестНеДолжен_ВыполнитьДействиеОбъектаТеста() Экспорт

	юТест.ПодробныеОписанияОшибок(Истина);

	БылоИсключение = Ложь;
	Попытка
		Действие = Новый Действие(ЭтотОбъект, "ПриватныйИсполнитель");
	Исключение
		БылоИсключение = Истина;
	КонецПопытки;

	Ожидаем.Что(БылоИсключение).Равно(Истина);

КонецПроцедуры

Процедура ТестДолжен_ВыполнитьДействиеОбъектаДвижка() Экспорт

	юТест.ПодробныеОписанияОшибок(Истина);

	Массив = Новый Массив;

	Действие = Новый Действие(Массив, "Добавить");
	Действие.Выполнить(1);

	Ожидаем.Что(Массив).ИмеетДлину(1);
	
КонецПроцедуры

Процедура ТестДолжен_ВыполнитьДействиеОбъектаДвижкаНаАнглийском() Экспорт

	юТест.ПодробныеОписанияОшибок(Истина);

	Массив = Новый Массив;

	Action = Новый Action(Массив, "Добавить");
	Action.Execute(1);

	Ожидаем.Что(Массив).ИмеетДлину(1);
	
КонецПроцедуры

Функция Исполнитель(Знач П1 = 100, Знач П2 = 10, Знач П3 = 1) Экспорт
	
	Возврат П1 + П2 + П3;

КонецФункции

Функция ПриватныйИсполнитель(Знач П1 = 1, Знач П2 = 10, Знач П3 = 100)
	
	Возврат П1 + П2 + П3;

КонецФункции
